buildscript {
    repositories {
        google()
        jcenter()
    }
    def buildGradleVersion = ext.has('buildGradlePluginVersion') ? ext.get('buildGradlePluginVersion') : '3.4.0'
    dependencies {
        classpath "com.android.tools.build:gradle:$buildGradleVersion"
    }
}

repositories {
    maven { url 'https://raw.githubusercontent.com/flussonic/flussonic-watcher-sdk-android/master/' }
    google()
    jcenter()
    maven {        url "$projectDir/../../../node_modules/react-native/android"    }
}

apply plugin: 'com.android.library'


def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def _reactNativeVersion = safeExtGet('reactNative', '+')
def _compileSdkVersion = safeExtGet('compileSdkVersion', 28)
def _buildToolsVersion = safeExtGet('buildToolsVersion', '28.0.3')
def _minSdkVersion = safeExtGet('minSdkVersion', 19)
def _targetSdkVersion = safeExtGet('targetSdkVersion', 28)

android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion _buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0
    // Если меняется библиотека (файл aar), aarVersionBuild должен быть равен versionCode библиотеки
    def aarVersionBuild = 49

    def resultVersionCode = versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + aarVersionBuild
    def resultVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

    defaultConfig {
        minSdkVersion _minSdkVersion
        targetSdkVersion _targetSdkVersion
        versionCode resultVersionCode
        versionName resultVersionName
        ndk.abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'

    }

    buildTypes {
        debug {
            versionNameSuffix '-DEBUG'
            debuggable true
        }
        release {
            versionNameSuffix ''
            debuggable false
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation "com.facebook.react:react-native:$_reactNativeVersion"
    implementation 'com.flussonic:watcher-sdk:1.6'
}
